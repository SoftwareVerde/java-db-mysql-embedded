group 'com.softwareverde'
version '2.0.2'

apply plugin: 'java'
apply plugin: 'java-library'

sourceCompatibility = 1.8

task makeJar(type: Jar) {
    manifest {
        attributes  'Implementation-Title': 'Java MySQL Embedded Database',
                    'Implementation-Version': version
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    api                 group: 'com.github.softwareverde',  name: 'java-db-mysql',      version: 'v4.0.4'
    api                 (group: 'com.github.softwareverde',  name: 'mariaDB4j',          version: 'v1.0.5') {
        exclude group: 'org.apache.logging.log4j',  module: '*'
        exclude group: 'ch.qos.logback',            module: '*'
        exclude group: 'org.springframework.boot',  module: '*'
        exclude group: 'javax.annotations',         module: '*'
        exclude group: 'javax.enterprise',          module: '*'
        exclude group: 'javax.inject',              module: '*'
        exclude group: 'org.apache.maven',          module: '*'
        exclude group: 'org.hamcrest',              module: '*'
        exclude group: 'org.slf4j',                 module: '*'
        // exclude group: 'org.springframework',       module: '*'
        // exclude group: 'commons-io',                module: '*'
        // exclude group: 'org.apache.commons',        module: '*'
    }
    implementation      group: 'com.github.softwareverde',  name: 'java-util',          version: 'v2.0.6'

    testImplementation  group: 'junit',                     name: 'junit',              version: '4.12'
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (! desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
